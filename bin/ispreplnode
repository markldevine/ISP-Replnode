#!/usr/bin/env raku

use ISP::Server::Reporter;

constant \KILO  = 1024;
constant \MEGA  = KILO  * 1024;
constant \GIGA  = MEGA  * 1024;
constant \TERA  = GIGA  * 1024;
constant \PETA  = TERA  * 1024;

sub bytes-to-unit (Int:D $bytes) {
    given $bytes {
        when $_ >= PETA { return ($bytes / PETA).fmt("%.1f P"); }
        when $_ >= TERA { return ($bytes / TERA).fmt("%.1f T"); }
        when $_ >= GIGA { return ($bytes / GIGA).fmt("%.1f G"); }
        when $_ >= MEGA { return ($bytes / MEGA).fmt("%.1f M"); }
        when $_ >= KILO { return ($bytes / KILO).fmt("%.1f K"); }
        default         { return $bytes.fmt("%s B");            }
    }
}

class ISP-Replnode-statistics {

    has Int $!source-objs               = 0;
    has Int $.source-objs-delta         = 0;
    has Int $!last-source-objs          = -1;

    method source-objs (Int :$source-objs) {
        return $!source-objs            without $source-objs;
        $!last-source-objs              = $source-objs unless $!last-source-objs >= 0;
        $!source-objs-delta             = $source-objs - $!last-source-objs;
        $!source-objs                   = $source-objs;
        $!last-source-objs              = $source-objs;
    }

    has Int $!target-objs               = 0;
    has Int $.target-objs-delta         = 0;
    has Int $!last-target-objs          = -1;

    method target-objs (Int :$target-objs) {
        return $!target-objs            without $target-objs;
        $!last-target-objs              = $target-objs unless $!last-target-objs >= 0;
        $!target-objs-delta             = $target-objs - $!last-target-objs;
        $!target-objs                   = $target-objs;
        $!last-target-objs              = $target-objs;
    }

    has Int $!pending-objs              = 0;
    has Int $.pending-objs-delta        = 0;
    has Int $!last-pending-objs         = -1;

    method pending-objs () {
        $!pending-objs                  = (self.source-objs - self.target-objs).abs;
        $!last-pending-objs             = $!pending-objs unless $!last-pending-objs >= 0;
        $!pending-objs-delta            = $!pending-objs - $!last-pending-objs;
        $!last-pending-objs             = $!pending-objs;
        return $!pending-objs
    }
}

class Reporter does ISP::Server::Reporter {

    has %!statistics;

    method process-rows (@replnodes) {
        my Str $node-name;                                  #                            Node Name: P_SSSSSS
        my Str $type;                                       #                                 Type: Bkup
        my Str $filespace;                                  #                       Filespace Name: /nfsmounts/P_SSSSSS
        my Str $fsid;                                       #                                 FSID: 1
        my Int $source-objs;                                # Objects on Source Replication Server: 440
        my Int $target-objs;                                # Objects on Target Replication Server: 436
        my Str $target-server;                              #            Target Replication Server: IIIIIIII05

        my $total-source-objs           = 0;
        my $total-source-objs-delta     = 0;
        my $total-target-objs           = 0;
        my $total-target-objs-delta     = 0;
        my $total-pending-objs          = 0;

        my $row;
        for @replnodes -> $replnode {
            $node-name                  = '';   $node-name      = $replnode{'Node Name'}.Str                                                    if $replnode{'Node Name'};
            $type                       = '';   $type           = $replnode{'Type'}.Str                                                         if $replnode{'Type'};
            $filespace                  = '';   $filespace      = $replnode{'Filespace Name'}.Str                                               if $replnode{'Filespace Name'};
            $fsid                       = '';   $fsid           = $replnode{'FSID'}.Str                                                         if $replnode{'FSID'};
            $source-objs                = 0;    $source-objs    = $replnode{'Objects on Source Replication Server'}.comb.grep(/\d/).join.Int    if $replnode{'Objects on Source Replication Server'};
            $target-objs                = 0;    $target-objs    = $replnode{'Objects on Target Replication Server'}.comb.grep(/\d/).join.Int    if $replnode{'Objects on Target Replication Server'};
            $target-server              = '';   $target-server  = $replnode{'Target Replication Server'}.Str                                    if $replnode{'Target Replication Server'};

            %!statistics{$node-name}{$fsid}{$type}              = ISP-Replnode-statistics.new   unless %!statistics{$node-name}{$fsid}{$type}:exists;

            %!statistics{$node-name}{$fsid}{$type}.source-objs(:$source-objs);
            %!statistics{$node-name}{$fsid}{$type}.target-objs(:$target-objs);

            $total-source-objs         += $source-objs;
            $total-source-objs-delta   += %!statistics{$node-name}{$fsid}{$type}.source-objs-delta;

            $total-target-objs         += $target-objs;
            $total-target-objs-delta   += %!statistics{$node-name}{$fsid}{$type}.target-objs-delta;

            my $pending-objs            = %!statistics{$node-name}{$fsid}{$type}.pending-objs;
            $total-pending-objs        += $pending-objs;

            $row                        = Array.new;
            $row.push:                  $node-name;
            $row.push:                  $type;
            $row.push:                  $filespace;
            $row.push:                  $fsid;
            $row.push:                  %!statistics{$node-name}{$fsid}{$type}.source-objs.Str.flip.comb(3).join(',').flip;
            $row.push:                  %!statistics{$node-name}{$fsid}{$type}.source-objs-delta.Str.flip.comb(3).join(',').flip;
            $row.push:                  %!statistics{$node-name}{$fsid}{$type}.target-objs.Str.flip.comb(3).join(',').flip;
            $row.push:                  %!statistics{$node-name}{$fsid}{$type}.target-objs-delta.Str.flip.comb(3).join(',').flip;
            $row.push:                  $pending-objs.Str.flip.comb(3).join(',').flip;
            
            my Rat $pct-complete        = 100.0;
            if $source-objs > 0 {
                $pct-complete           = ($target-objs / $source-objs) * 100;
                $pct-complete           = 99.9 if 99.9 < $pct-complete < 100.0;
            }
            $row.push:                  $pct-complete.fmt("%.1f%%");
            $row.push:                  $target-server;
            self.table.add-row:         $row;
        }
        $row                            = [ '~', ' ', ' ', ' ', '-------', '-------', '-------', '-------', '-------', '-------', ' ', ];
        self.table.add-row:             $row;
        $row                            = [ "~ TOTALS", ' ', ' ', ' ', ];
        $row.push:                      bytes-to-unit($total-source-objs);
        $row.push:                      bytes-to-unit($total-source-objs-delta);
        $row.push:                      bytes-to-unit($total-target-objs);
        $row.push:                      bytes-to-unit($total-target-objs-delta);
        $row.push:                      bytes-to-unit($total-pending-objs);
        my Rat $pct-complete            = ($total-target-objs / $total-source-objs) * 100;
        if 99.9 < $pct-complete < 100.0 {
            $pct-complete               = 99.9;
        }
        $row.push:                      $pct-complete.fmt("%.1f%%");
        $row.push:                      ' ';
        self.table.add-row:             $row;
    }
}

sub MAIN (
    Str:D   :$isp-server!,                          #= ISP server name
    Str:D   :$isp-admin!,                           #= ISP server name
    Int:D   :$interval      where * >= 5    = 58,   #= Refresh every --interval seconds (minimum 10s)
    Int:D   :$count                         = 1,    #= Number of refreshes (0 is infinity)
    Bool    :$grid,                                 #= Full table grid
    Bool    :$clear,                                #= Clear the screen with each iteration
    Str     :$node,                                 #= ISP NODE name or NODEGROUP
) {
    my @command         = ['QUERY', 'REPLNODE'];
    if $node {
        @command.push: $node;
    }
    else {
        @command.push: '*';
    }
    my @fields;
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Node Name'),        :alignment('l'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Type'),             :alignment('l'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Filespace Name'),   :alignment('l'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('FSID'),             :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Source Objs'),      :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Source Δ'),       :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Target Objs'),      :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Target Δ'),       :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Pending Objs'),     :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Completed'),        :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Target Server'),    :alignment('l'));
    my $reporter    = Reporter.new(
                                    :$isp-server,
                                    :$isp-admin,
                                    :$count,
                                    :$grid,
                                    :$clear,
                                    :$interval,
                                    :title('IBM Spectrum Protect: ' ~ $isp-server ~ ' Node Replication'),
                                    :@command,
                                    :@fields,
                                    :sort-by('Node Name'),
                                  );
    $reporter.loop;
}

=finish
